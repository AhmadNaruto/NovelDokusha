import org.jetbrains.kotlin.gradle.tasks.KotlinCompile
import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    `kotlin-dsl`
}

group = "my.noveldokusha.convention.plugin"

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

tasks.withType<KotlinCompile>().configureEach {
    compilerOptions {
        jvmTarget.set(JvmTarget.JVM_21)
    }
}

dependencies {
    compileOnly(libs.android.gradlePlugin)
    compileOnly(libs.android.tools.common)
    compileOnly(libs.kotlin.gradlePlugin)
    compileOnly(libs.ksp.gradlePlugin)
}

tasks {
    validatePlugins {
        enableStricterValidation = true
        failOnWarning = true
    }
}

gradlePlugin {
    plugins {
        register("androidApplication") {
            id = "noveldokusha.android.application"
            implementationClass =
                "NoveldokushaAndroidApplicationBestPracticesConventionPlugin"
        }
        register("androidLibrary") {
            id = "noveldokusha.android.library"
            implementationClass =
                "NoveldokushaAndroidLibraryBestPracticesConventionPlugin"
        }
        register("androidCompose") {
            id = "noveldokusha.android.compose"
            implementationClass =
                "NoveldokushaAndroidComposeBestPracticesConventionPlugin"
        }
    }
}
